buildscript {
	ext { springBootVersion = '1.5.7.RELEASE' }
	repositories { mavenCentral() }
	dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'employee-service'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories { mavenCentral() }

configurations {
	jaxb
	// exclude logging started to switch from logback to log4j2
	all*.exclude module: 'spring-boot-starter-logging'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


task genJaxb {
	def legacy_user_port = project.hasProperty('legacy_user_port') ? project.legacy_user_port : '8080'

	println("${legacy_user_port}")
	ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
	ext.classesDir = "${buildDir}/classes/jaxb"
	ext.schema = "http://localhost:${legacy_user_port}/cloud-demo/ws/users.wsdl"

	outputs.dir classesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			mkdir(dir: classesDir)

			xjc(destdir: sourcesDir, schema: schema,
					package: "com.slalom.cloud.legacy.users.client.jaxb") {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}

			javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
					debugLevel: "lines,vars,source",
					classpath: configurations.jaxb.asPath) {
				src(path: sourcesDir)
				include(name: "**/*.java")
				include(name: "*.java")
			}

			copy(todir: classesDir) {
				fileset(dir: sourcesDir, erroronmissingdir: false) {
					exclude(name: "**/*.java")
				}
			}
		}
	}
}

sourceSets.main.java.srcDir("${buildDir}/generated-sources/jaxb")
tasks.compileJava.dependsOn("genJaxb")
tasks.eclipse.dependsOn("genJaxb")

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.cloud:spring-cloud-starter-hystrix-dashboard')
	compile('org.springframework.cloud:spring-cloud-starter-hystrix')
//	compile('org.springframework.cloud:spring-cloud-starter-vault-config')
//	compile('org.springframework.cloud:spring-cloud-starter-consul-all')
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile('org.springframework.cloud:spring-cloud-starter-config')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-log4j2')
	compile('org.springframework.ws:spring-ws-core')
	compile('org.springframework.security:spring-security-core:4.2.1.RELEASE')
	compile('org.springframework.security:spring-security-web:4.2.1.RELEASE')
	compile('org.springframework.security:spring-security-config:4.2.1.RELEASE')

	compile('org.springframework.session:spring-session:1.3.1.RELEASE')
	compile('org.springframework.boot:spring-boot-starter-data-redis')
	compile('biz.paluch.redis:lettuce:4.3.1.Final')
	compile('jstl:jstl:1.2')

	compile(group: 'mysql', name: 'mysql-connector-java', version: '5.1.35')
	compile(group: 'com.h2database', name: 'h2', version: '1.4.192')

	compile(files(genJaxb.classesDir).builtBy(genJaxb))

  jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"

	compile(group: 'commons-logging', name: 'commons-logging', version: '1.2')
	compile(group: 'org.apache.commons', name: 'commons-lang3', version: '3.4')
	compile(group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.0.1')

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

ext {
	springCloudVersion = 'Dalston.RELEASE'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

eclipse {
	classpath {
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}
//, '-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=9950,suspend=n', '-Derac.serverLevel=desktop', '-DLOG_LEVEL=debug', '-DLOG_PATH=c:/builds/cloud-demo/logs'
bootRun {
	jvmArgs = ['-Dspring.profiles.active=local,consul']
}

task afterEclipseImport {
    dependsOn genJaxb
}
