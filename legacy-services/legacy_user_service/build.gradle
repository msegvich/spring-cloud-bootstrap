buildscript {
	ext {
		springBootVersion = '1.5.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

group = "com.slalom.legacy.cloud.cloud-demo"
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	providedRuntime
	jaxb

	// exclude logging started to switch from logback to log4j2
	all*.exclude module: 'spring-boot-starter-logging'
}

task genJaxb {
	ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
	ext.schema = "src/main/resources/user.xsd"

	outputs.dir sourcesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)

			xjc(destdir: sourcesDir, schema: schema) {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}
		}
	}
}

sourceSets.main.java.srcDir("${buildDir}/generated-sources/jaxb")
tasks.compileJava.dependsOn("genJaxb")
tasks.eclipse.dependsOn("genJaxb")

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-web-services')
	compile('org.springframework.boot:spring-boot-starter-log4j2')
	testCompile('org.springframework.boot:spring-boot-starter-test')

	compile(group: 'commons-logging', name: 'commons-logging', version: '1.2')
	compile(group: 'org.apache.commons', name: 'commons-lang3', version: '3.4')
	compile(group: 'com.h2database', name: 'h2', version: '1.4.192')
	compile(group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.0.1')

	compile("wsdl4j:wsdl4j:1.6.1")
	jaxb("org.glassfish.jaxb:jaxb-xjc:2.2.11")

	runtime('org.springframework.boot:spring-boot-devtools')
	runtime('com.h2database:h2')
}

bootRun {
	jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=9000,suspend=n', '-Derac.serverLevel=desktop', '-DLOG_LEVEL=debug', '-DLOG_PATH=c:/builds/cloud-demo/logs']
}
